plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

apply plugin: 'java-library'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group 'fr.flowarg'
version '3.2.4'
archivesBaseName = 'openlauncherlib'

compileJava {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
    options.encoding = 'UTF-8'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

jar {
    from "LICENSE"
    from "LICENSE.LESSER"
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar
            artifact tasks.javadocJar
            
            pom {
                groupId = project.group
                version = project.version
                artifactId = archivesBaseName
                name = 'OpenLauncherLib'
                description = 'A simple modern java launching library with minecraft support '
                url = 'https://github.com/FlowArg/OpenLauncherLib'

                issueManagement {
                    system = 'github'
                    url = 'https://github.com/FlowArg/OpenLauncherLib/issues'
                }

                scm {
                    connection = 'scm:git:git://github.com/FlowArg/OpenLauncherLib.git'
                    developerConnection = 'scm:git:ssh://github.com:FlowArg/OpenLauncherLib.git'
                    url = 'http://github.com/FlowArg/OpenLauncherLib/tree/master'
                }

                licenses {
                    license {
                        name = 'GNU GENERAL PUBLIC LICENSE 3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                    
                    license {
                        name = 'GNU LESSER GENERAL PUBLIC LICENSE 3.0'
                        url = 'https://www.gnu.org/licenses/lgpl-3.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'flowarg'
                        name = 'Flow Arg'
                        email = 'flow@flowarg.fr'
                    }
                }
            }
        }
    }
    
    repositories {
        maven {
            credentials {
                username System.getenv("OSSRH_USERNAME")
                password System.getenv("OSSRH_PASSWORD")
            }
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
        }
    }    
}

signing {
    def signingKey = System.getenv("GPG_PRIVATE_KEY")
    def signingPassword = System.getenv("GPG_PASSPHRASE")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.json:json:20211205'
    implementation 'org.jetbrains:annotations:20.1.0'
}

shadowJar {
    classifier 'shaded'
}
